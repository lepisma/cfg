#!/usr/bin/env fish

function usage
    echo "Usage:"
    echo "  scr (window|rectangle|screen)"
end

function copy_and_save
    if test $status -eq 0
        copyq copy image/png - < $argv[1]
        notify-send "Saved: "$argv[1]
    else
        notify-send "Screenshot error" -u "Critical"
    end
end

if test (count $argv) -ne 1
    usage
    exit 1
end

set file_name ~/Pictures/screenshot-(date +"%Y-%m-%d-%H-%M-%S").png

switch $argv[1]
    case window
        maim -st 9999999 | convert - \( +clone -background black -shadow 80x5+1+1 \) +swap -background none -layers merge +repage $file_name
        copy_and_save $file_name
    case rectangle
        maim -s $file_name
        copy_and_save $file_name
    case screen
        # Might want to handle multiple screens differently
        maim $file_name
        copy_and_save $file_name
    case "*"
        usage
        exit 1
end
